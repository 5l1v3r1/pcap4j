/*_##########################################################################
  _##
  _##  Copyright (C) 2016 Pcap4J.org
  _##
  _##########################################################################
*/

archivesBaseName = 'pcap4j-packetfactory-static'
description = 'Static Packet Factory.'
ext.moduleName = 'org.pcap4j.packetfactory.statik'

configurations {
  packetTests {
    transitive = false
  }
}

dependencies {
  implementation project(':pcap4j-core')
  packetTests project(path: ':pcap4j-packettest', configuration: 'testJar')
  testRuntimeOnly "ch.qos.logback:logback-core:${logbackVer}"
  testRuntimeOnly "ch.qos.logback:logback-classic:${logbackVer}"
  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVer}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVer}"
}

task extractTests(type: Copy, dependsOn: [testClasses, ':pcap4j-packettest:jarTests']) {
  from {
    configurations.packetTests.collect { zipTree it }
  }
  into sourceSets.test.output.classesDirs[0]
}

test {
  dependsOn extractTests
  systemProperties System.getProperties().findAll({
    it.key.startsWith 'org.pcap4j.' or it.key == 'jna.library.path' or it.key == 'jna.nosys'
  })
  systemProperty 'org.pcap4j.test.packet.AbstractPacketTest.resourceDir', sourceSets.test.output.classesDirs[0]
  systemProperty 'org.pcap4j.test.packet.AbstractPacketTest.tmpDir', temporaryDir
}

testJre8 {
  dependsOn extractTests
  systemProperties System.getProperties().findAll({
    it.key.startsWith 'org.pcap4j.' or it.key == 'jna.library.path' or it.key == 'jna.nosys'
  })
  systemProperty 'org.pcap4j.test.packet.AbstractPacketTest.resourceDir', sourceSets.test.output.classesDirs[0]
  systemProperty 'org.pcap4j.test.packet.AbstractPacketTest.tmpDir', temporaryDir
}
